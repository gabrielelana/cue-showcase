* What is CUE?
Data validation language and inference engine
- Data validation
- Data templating
- Configuration
- Querying
- Code generation

Originally from Google (improved upon GCL = Google Configuration Language)

* A Configuration Language

You can consider it as a superset of JSON

#+BEGIN_SRC cue :tangle src/constant.cue
foo: 5 + 5
bar: "foo is \(foo)"
#+END_SRC

Which can be exported to various formats

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue eval src/constant.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
foo: 10
bar: "foo is 10"
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/constant.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "foo": 10,
    "bar": "foo is 10"
}
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out yaml src/constant.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
foo: 10
bar: foo is 10
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out toml src/constant.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
bar = 'foo is 10'
foo = 10
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out textproto src/constant.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
foo: 10
bar: "foo is 10"
#+end_OUTPUT

* A Single Source of Truth for Many Configuration Files

It's annoying when you have a few "configuration" values that must be replicated
in different places but you want to duplicate them but you want to keep a single
source of truth.

With CUE you can keep all those value in a single CUE file and prepare different
structures with a pontential subset of those values ready to be exported in
different formats.

#+BEGIN_SRC cue :tangle src/source-of-truth.cue
// single value, single source of truth
servicePort: 3000

// will be exported as environment variable
environment: {
    PORT: servicePort
}

// will be exported as JSON configuration file
configuration: {
    port: servicePort
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json -e configuration src/source-of-truth.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "port": 3000
}
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out toml -e environment src/source-of-truth.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
PORT = 3000
#+end_OUTPUT

* A Data Validation Language

#+BEGIN_SRC cue :tangle src/person.cue
// a single person defined in CUE
person: {
    firstName: "Gabriele"
    lastName: "Lana"
    occupation: "software developer"
}
#+END_SRC

We want to make sure that *all* of the people have the same fields

#+BEGIN_SRC cue :tangle src/person-schema.cue
// this is also CUE
person: {
    firstName!: string
    lastName!: string
    occupation!: string
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue eval src/person-schema.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
person: {
    firstName!:  string
    lastName!:   string
    occupation!: string
}
#+end_OUTPUT

CUE will let us to put them together and so to ask CUE if they are compatibile
with each other.

By asking this in the end you are using one CUE value (~person~ defined in
~src/person-schema.cue~) to validate another CUE value (~person~ defined in
~src/person.cue~)

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/person-schema.cue src/person.cue
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "person": {
        "firstName": "Gabriele",
        "lastName": "Lana",
        "occupation": "software developer"
    }
}
#+end_OUTPUT

What if something is not "compatibile" with the schema?

#+BEGIN_SRC cue :tangle src/bad-person.cue
// a single person defined in CUE
person: {
    firstName: "Gabriele"
    lastName: "Lana"
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue vet -c src/person-schema.cue src/bad-person.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
person.occupation: field is required but not present:
    ./src/person-schema.cue:5:5
#+end_OUTPUT

* A Data Validation Language (2)

How does it work? CUE has no conventional values, every value is a constraint,
constraints are an unordered set, order doesn't matter

#+BEGIN_SRC cue :tangle src/constraints.cue
foo: string
foo: =~ "[0-9]{2}$"
foo: "the answer is 42"
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/constraints.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "foo": "the answer is 42"
}
#+end_OUTPUT

This means that constraints can be added at any time, immagine the financial
department requiring the vat number for the person schema

#+BEGIN_SRC cue :tangle src/person-financial.cue
person: {
    vat!: string & =~ "^IT"
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue vet -c src/person-schema.cue src/person-financial.cue src/person.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
person.vat: field is required but not present:
    ./src/person-financial.cue:2:5
#+end_OUTPUT

#+BEGIN_SRC cue :tangle src/complete-person.cue
person: {
    firstName: "Gabriele"
    lastName: "Lana"
    occupation: "software developer"
    vat: "IT08937100967"
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/person-schema.cue src/person-financial.cue src/complete-person.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "person": {
        "firstName": "Gabriele",
        "lastName": "Lana",
        "occupation": "software developer",
        "vat": "IT08937100967"
    }
}
#+end_OUTPUT

You can go further, not every person has a vat number

#+BEGIN_SRC cue :tangle src/person-financial-smart.cue
person: {
        consultant: true | false
        if consultant == true {
                vat!: string & =~ "^IT"
        }
}
#+END_SRC

#+BEGIN_SRC cue :tangle src/person-financial-smart.cue
person: {
        consultant: true | false
        if consultant == true {
                vat!: string & =~ "^IT"
        }
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue eval src/person-schema.cue src/person-financial-smart.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
person: {
    consultant: true | false
    if consultant == true {
        vat!: =~"^IT"
    }
} & {
    firstName!:  string
    lastName!:   string
    occupation!: string
}
#+end_OUTPUT

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/person-schema.cue src/person-financial-smart.cue src/complete-person.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
person: unresolved disjunction true | false (type bool):
    ./src/person-financial-smart.cue:3:12
#+end_OUTPUT

#+BEGIN_SRC cue :tangle src/truly-complete-person.cue
person: {
    firstName: "Gabriele"
    lastName: "Lana"
    occupation: "software developer"
    consultant: true
    vat: "IT08937100967"
}
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/person-schema.cue src/person-financial-smart.cue src/truly-complete-person.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "person": {
        "firstName": "Gabriele",
        "lastName": "Lana",
        "consultant": true,
        "vat": "IT08937100967",
        "occupation": "software developer"
    }
}
#+end_OUTPUT

Fields can be referred

#+BEGIN_SRC cue :tangle src/field-reference.cue
count: {
    // useless but cool
    min!: uint & <=count.max
    max!: uint & >=count.min
}

// try to break the mutual constraint
// try to remove a field
// try to add a field
count: min: 1
count: max: 5
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/field-reference.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "count": {
        "min": 1,
        "max": 5
    }
}
#+end_OUTPUT

* Data Templating

#+BEGIN_SRC cue :tangle src/generate-people.cue
#Person: {
    firstName!: string & =~"^[A-Z].*$"
    lastName!: string & =~"^[A-Z].*$"
    fullName: "\(firstName) \(lastName)"
    occupation!: string
    consultant: bool | *false
    if consultant == true {
        vat!: =~"^IT"
    }
}

people: [...#Person]

people: [{
    firstName: "Gabriele"
    lastName: "Lana"
    occupation: "software developer"
    consultant: true
    vat: "IT08937100967"
}, {
    firstName: "Mario"
    lastName: "Rossi"
    occupation: "baker"
}]
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/generate-people.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "people": [
        {
            "firstName": "Gabriele",
            "lastName": "Lana",
            "fullName": "Gabriele Lana",
            "occupation": "software developer",
            "consultant": true,
            "vat": "IT08937100967"
        },
        {
            "firstName": "Mario",
            "lastName": "Rossi",
            "fullName": "Mario Rossi",
            "occupation": "baker",
            "consultant": false
        }
    ]
}
#+end_OUTPUT

More useful when things are repeated many times

#+BEGIN_SRC cue :tangle src/generate-servers.cue
#DB: "postgresql" | =~"db$"
#CheckPostgreSQL: close({for k, v in services {"\(k)": true}}) & {"postgresql": true}

services: {
        nginx: _
        blizard: _
        "otel-collector": command: "exec otel"
        boltdb: replicas: 3
        postgresql: replicas: 5
}

services: [Name=_]: {
        name:     Name
        command:  string | *"exec \(Name)"
        replicas: uint | *1
}

services: [#DB]: replicas: >=3
#+END_SRC

#+BEGIN_SRC shell :results raw :wrap OUTPUT
cue export --out json src/generate-servers.cue 2>&1
#+END_SRC

#+RESULTS:
#+begin_OUTPUT
{
    "services": {
        "nginx": {
            "name": "nginx",
            "command": "exec nginx",
            "replicas": 1
        },
        "blizard": {
            "name": "blizard",
            "command": "exec blizard",
            "replicas": 1
        },
        "otel-collector": {
            "name": "otel-collector",
            "command": "exec otel",
            "replicas": 1
        },
        "boltdb": {
            "name": "boltdb",
            "command": "exec boltdb",
            "replicas": 3
        },
        "postgresql": {
            "name": "postgresql",
            "command": "exec postgresql",
            "replicas": 5
        }
    }
}
#+end_OUTPUT

* Example: Avro

First the basic definition of the Avro schema

#+BEGIN_SRC cue :tangle src/avro-base.cue
#Schema: #TypeName | #Union | #Record | #Enum | #Array

#Name: =~#"^([A-Za-z_][A-Za-z0-9_]*)(\.([A-Za-z_][A-Za-z0-9_]*))*$"#

#TypeName: #PrimitiveType | #DefinedType
#TypeName: #Name

#PrimitiveType: "null" | "boolean" | "int" | "long" | "float" | "double" | "bytes" | "string"

#DefinedType: string

#Union: [... #Schema]

#Definition: {
    type!: string
    name!: #DefinedType
    namespace?: =~#"^([A-Za-z_][A-Za-z0-9_]*)(\.([A-Za-z_][A-Za-z0-9_]*))*"#
    aliases?: [...string]
    doc?: string
    ...
}

#Record: #Definition & {
    type!: "record"
    doc?: string
    fields!: [... #Field]
}

#Enum: #Definition & {
    type!: "enum"
    name!: string
    symbols!: [... #Name]
    default?: #Name
    if !list.Contains(symbols, default) {
        default?: _|_
    }
}

#Array: {
    type!: "array"
    items!: #Schema
    default!: []
}

// ...
#+END_SRC

Then you can add you own constraint, for you own domain, we called them "briks"

#+BEGIN_SRC cue :tangle src/avro-briks.cue
#Money: avro.#Record & {
    type!: "record"
    name!: "Money"
    fields!: [{
        name!: "amount"
        type!: "int"
    }, {
        name!: "currency"
        type!: avro.#Enum & {
            name!: "Currency"
            symbols!: ["EUR"]
            default!: "EUR"
        }
    }]
}

#DateTime: avro.#Field & {
    name!: avro.#Name,
    type!: "string"
}

// ...
#+END_SRC

The you can use it

#+BEGIN_SRC cue :tangle src/avro-consents.cue
#ConsentFieldNames: ["consents", "given", "denied"]

#ConsentFields: {
    for _name in #ConsentFieldNames {
        "\(_name)": {
            name: "\(_name)_Consent"
            type: "record"
            fields: [
                {name: "name", type: "string"},
                {name: "owner", type: avro.#Enum & {
                    name: "\(_name)_Owner"
                    symbols: [
                        "UNKNOWN",
                        "MARKETING",
                        "BROADBAND",
                        "ENERGY",
                        "INSURANCE_MOTOR",
                        "INSURANCE_LIFE",
                        "INSURANCE_PROFESSIONAL",
                        "INSURANCE_PET",
                        "LONG_TERM_RENTAL",
                        "MORTGAGE",
                        "LOAN",
                        "PERSONAL_AREA",
                ],
                    default: "UNKNOWN"
                }},
                {name: "isMandatory", type: "boolean"},
                {name: "given", type: "boolean"},
                briks.#DateTime & {name: "updatedAt"},
        ]}
    }
}

#Identity: {
    name: "Identity"
    type: "record"
    fields: [
        {name: "email", type: "string"},
        {name: "facileUserId", type: ["null", "string"]},
    ]
}

avro.#Schema & {
    name: "Consents"
    fields: [
        {name: "identity", type: #Identity},
        briks.#DateTime & {name: "createdAt"},
        briks.#DateTime & {name: "updatedAt"},
        for _name in #ConsentFieldNames {
            {name: _name, type: {
                type: "array",
                items: #ConsentFields[_name],
                default: [],
            }}
        }
    ]
}
#+END_SRC

#+BEGIN_SRC json
{
  "name": "Consents",
  "type": "record",
  "fields": [
    {
      "name": "identity",
      "type": {
        "name": "Identity",
        "type": "record",
        "fields": [
          {
            "name": "email",
            "type": "string"
          },
          {
            "name": "facileUserId",
            "type": ["null", "string"]
          }
        ]
      }
    },
    {
      "name": "createdAt",
      "type": "string"
    },
    {
      "name": "updatedAt",
      "type": "string"
    },
    {
      "name": "consents",
      "type": {
        "type": "array",
        "default": [],
        "items": {
          "name": "Consent",
          "type": "record",
          "fields": [
            {
              "name": "name",
              "type": "string"
            },
            {
              "name": "owner",
              "type": {
                "name": "Owner",
                "type": "enum",
                "symbols": [
                  "UNKNOWN",
                  "MARKETING",
                  "BROADBAND",
                  "ENERGY",
                  "INSURANCE_MOTOR",
                  "INSURANCE_LIFE",
                  "INSURANCE_PROFESSIONAL",
                  "INSURANCE_PET",
                  "LONG_TERM_RENTAL",
                  "MORTGAGE",
                  "LOAN",
                  "PERSONAL_AREA"
                ],
                "default": "UNKNOWN"
              }
            },
            {
              "name": "isMandatory",
              "type": "boolean"
            },
            {
              "name": "updatedAt",
              "type": "string"
            },
            {
              "name": "given",
              "type": "boolean"
            }
          ]
        }
      }
    },
    {
      "name": "given",
      "type": {
        "type": "array",
        "default": [],
        "items": {
          "name": "Consent",
          "type": "record",
          "fields": [
            {
              "name": "name",
              "type": "string"
            },
            {
              "name": "owner",
              "type": {
                "name": "Owner",
                "type": "enum",
                "symbols": [
                  "UNKNOWN",
                  "MARKETING",
                  "BROADBAND",
                  "ENERGY",
                  "INSURANCE_MOTOR",
                  "INSURANCE_LIFE",
                  "INSURANCE_PROFESSIONAL",
                  "INSURANCE_PET",
                  "LONG_TERM_RENTAL",
                  "MORTGAGE",
                  "LOAN",
                  "PERSONAL_AREA"
                ],
                "default": "UNKNOWN"
              }
            },
            {
              "name": "isMandatory",
              "type": "boolean"
            },
            {
              "name": "updatedAt",
              "type": "string"
            },
            {
              "name": "given",
              "type": "boolean"
            }
          ]
        }
      }
    },
    {
      "name": "denied",
      "type": {
        "type": "array",
        "default": [],
        "items": {
          "name": "Consent",
          "type": "record",
          "fields": [
            {
              "name": "name",
              "type": "string"
            },
            {
              "name": "owner",
              "type": {
                "name": "Owner",
                "type": "enum",
                "symbols": [
                  "UNKNOWN",
                  "MARKETING",
                  "BROADBAND",
                  "ENERGY",
                  "INSURANCE_MOTOR",
                  "INSURANCE_LIFE",
                  "INSURANCE_PROFESSIONAL",
                  "INSURANCE_PET",
                  "LONG_TERM_RENTAL",
                  "MORTGAGE",
                  "LOAN",
                  "PERSONAL_AREA"
                ],
                "default": "UNKNOWN"
              }
            },
            {
              "name": "isMandatory",
              "type": "boolean"
            },
            {
              "name": "updatedAt",
              "type": "string"
            },
            {
              "name": "given",
              "type": "boolean"
            }
          ]
        }
      }
    }
  ]
}
#+END_SRC

* Example: OpenAPI

Same thing, basic definition of the OpenAPI specifications (the specification of
the specification ðŸ˜…)

#+BEGIN_SRC cue :tangle src/openapi-specification.cue
#OpenAPI: {
    openapi!: "3.1.0"
    info!: {
        title!:       string & !=""
        summary!:     string & !=""
        description!: string & !=""
        version!:     =~"^[0-9]+.[0-9]+.[0-9]+$"
    }
    servers!: [...#Server]
    paths!: [=~"^/.*$"]: #PathItem
    ...
}

#Server: {
    url!:         string & !=""
    description?: string
}

#PathItem: {
    summary?:     string
    description?: string
    [#Method]:    #Operation
    ...
}

#Operation: {
    tags?: [string & !=""]
    summary!:     string & !=""
    description?: string
    operationId!: string
    parameters?: [...#Parameter]
    requestBody?: #RequestBody
    responses?: [#ResponseStatusCode]: #Response
    deprecated?: bool | *false
    ...
}

// ...

// Also constraint not part of the specification but the specific
// domain/application

#ValidationError: {
    description: "Request validation error"
    content: {
        "application/problem+json": {
            schema: {
                type: "object"
                properites: {
                    status: {
                        type:  "number"
                        const: 400
                    }
                    type: type: "string"
                    code: {
                        type:  "string"
                        const: "REQUEST_VALIDATION_ERROR"
                    }
                    title: type: "string"
                    errors: {
                        type: "array"
                        items: {
                            type: "object"
                            properties: {
                                requestLocation: type: "string"
                                locationPath: type:    "string"
                                messages: {
                                    type: "array"
                                    items: type: "string"
                                }
                            }
                            required: ["requestLocation", "locationPath", "messages"]
                        }
                    }
                }
            }
            example: {
                status: 400
                code:   "REQUEST_VALIDATION_ERROR"
                title:  "Request Validation Error"
                errors: [{
                    requestLocation: "body"
                    locationPath:    "inc"
                    message: ["Expected number given string"]
                }]
            }
        }
    }
}
#+END_SRC

#+BEGIN_SRC cue :tangle src/openapi-use.cue
oas.#OpenAPI

info: {
    version: "0.0.1"
    title:   "MyApp OpenAPI Specification"
    summary: "MyApp OpenAPI Specification"
    description: """
                 My App OpenAPI Specification
                 """
}

// NOTE: you can define special constraints for your domain
paths: [Route=string]: [oas.#Method]: responses: "500": oas.#UnhandledError
paths: [Route=string]: [oas.#Method]: responses: "503": oas.#ServiceNotAvailableError
paths: [Route=string]: "post": responses: "400": oas.#ValidationError

paths: {
    "/ping": "get":                           #Ping
    "/users/login": "post":                   #UserLogin
    "/me/sessions/latest/{n}": "get":         #LatestSessions
    "/me/sessions/between/{range}": "get":    #SessionsBetweenDays
    "/me/check-in": "post":                   #CheckIn
    "/me": "get":                             #Me
}

#Me: {
    operationId: "Me"
    summary:     "Retrieve personal information."
    responses: {
        "200": {
            description: "Current user personal information."
            content: {
                "application/json": {
                    schema: #MeResultBodyOk
                }
            }
        }
        "400": oas.#ValidationError
        "401": oas.#AuthenticationError
        "500": oas.#UnhandledError
        "503": oas.#ServiceNotAvailableError
    }
}

// ...
#+END_SRC


* What's Next?
- [[https://github.com/cue-labs/cue-by-example][Cue By Example]]
- [[https://cuelang.org/docs/tour/][Official documentation]]
- [[https://cuetorials.com][Tutorial from practitioners]]
- [[https://github.com/search?q=lang%3Acue+&type=code][Seach examples on GitHub ðŸ¥²]]
